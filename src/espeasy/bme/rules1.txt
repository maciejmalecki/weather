on System#Boot do
  AsyncEvent,evtStart
endon

on System#Wake do
  AsyncEvent,evtStart
endon

on MQTT#Connected do
  Let,2,%unixday_sec%-%v1%
  Let,4,1
  if %v5%>0 // dont publish if no BME readings
    AsyncEvent,evtPublish
  endif
endon

on MQTT#Disconnected do
  AsyncEvent,evtNext
endon

on weather#temp do
  Let,5,1
  if %v4%>0 // dont publish if MQTT not connected
    AsyncEvent,evtPublish
  endif
endon

on evtStart do
  TaskValueSet,3,1,1
  Let,1,%unixday_sec%
  if [Plugin#GPIO#Pinstate#14]=1 // check for config mode
    TimerSet,2,1
  else
    if [vcc#vcc]<3.20 // low power protection
      DeepSleep,4294 // max sleep time
    endif
    TaskValueSet,3,1,0
  endif
  TimerSet,3,30 // only 30sec waiting time for net
endon

on evtPublish do
  Let,3,%unixday_sec%-%v1%
  Publish,%sysname%/json,'{"name":"%sysname%","vcc":[vcc#vcc],"status":[dummy#mode],"t0":%v2%,"t1":%v3%,"temp":[weather#temp],"hum":[weather#hum],"press":[weather#press]}'
  AsyncEvent,evtNext
endon

on evtNext do // next step depending on the mode
  if [dummy#mode]=0
    AsyncEvent,evtDeepSleep
  else
    TimerSet,1,60
  endif
endon

on evtDeepSleep do // normal mode, go to sleep, power off
    DeepSleep,900
endon

on Rules#Timer=1 do // config mode
  AsyncEvent,evtPublish
endon

on Rules#Timer=2 do // confirm config mode
  TaskValueSet,3,1,[Plugin#GPIO#Pinstate#14]
  TimerSet,4,600
endon

on Rules#Timer=3 do // dont wait for net too long
  AsyncEvent,evtNext
endon

on Rules#Timer=4 do // auto leave config
    AsyncEvent,evtDeepSleep
endif
